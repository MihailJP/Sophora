#!/usr/local/bin/fontforge

if ($argc < 5)
	Print("Usage: fontforge -script "+$0+" source-file stroke-width weight-name target-file [subfamily-suffix]")
	Quit(1)
endif
if ($argc <= 5)
	weightcat = ""
	weightsuffix = ""
else
	weightcat = " "
	weightsuffix = $5
endif

WorkingScale=10
if (Strtol($2) > 50)
	WorkingScale=5
endif

Open($1)

if ($3 == "Black")
	SetPanose(2, 10); SetOS2Value("Weight", 900)
elseif ($3 == "Heavy")
	SetPanose(2, 9); SetOS2Value("Weight", 800)
elseif ($3 == "Bold")
	SetPanose(2, 8); SetOS2Value("Weight", 700)
elseif ($3 == "Demi-Bold")
	SetPanose(2, 7); SetOS2Value("Weight", 600)
elseif ($3 == "Medium")
	SetPanose(2, 6); SetOS2Value("Weight", 500)
elseif ($3 == "Book")
	SetPanose(2, 5); SetOS2Value("Weight", 400)
elseif ($3 == "Light")
	SetPanose(2, 4); SetOS2Value("Weight", 300)
elseif ($3 == "Extra-Light")
	SetPanose(2, 3); SetOS2Value("Weight", 200)
elseif ($3 == "Thin")
	SetPanose(2, 2); SetOS2Value("Weight", 100)
endif

SelectWorthOutputting()
Scale(WorkingScale*100,0,0)
AddExtrema()
RoundToInt()
Simplify()
ExpandStroke(Strtol($2)*WorkingScale,0,2,0,1)
foreach
	if (GlyphInfo("Color") == 0xff00ff)
		UnlinkReference()
		CorrectDirection()
		SetGlyphColor(-2)
	endif
endloop
RemoveOverlap()
Scale(100/WorkingScale,0,0)
SetFontNames(Strsub($fontname,0,Strrstr($fontname,"-"+$weight))+"-"+$3,$familyname,$familyname+" "+$3+weightcat+weightsuffix,$3+weightsuffix)
Save($4)
